[[http://melpa.org/#/emms-player-simple-mpv][file:http://melpa.org/packages/emms-player-simple-mpv-badge.svg]]
[[http://stable.melpa.org/#/emms-player-simple-mpv][file:http://stable.melpa.org/packages/emms-player-simple-mpv-badge.svg]]

* emms-player-simple-mpv

  emms-player-simple-mpv.el is an extension of emms-player-simple.el for mpv JSON IPC.
  It provides basic functions for interface defined in EMMS ( [[https://www.gnu.org/software/emms/][The Emacs Multimedia System]] ).

  emms-player-simple-mpv-control-functions.el provides other functions to control mpv.

  + [[#requirements][Requirements]]
  + [[#setup][Setup]]
  + [[#usage][Usage]]
    + [[#customization][Customization]]
    + [[#an-example-of-setting-for-tq-event-hooks][An example of setting for TQ event hooks]]
    + [[#an-example-of-setting-for-hydra--httpsgithubcomabo-abohydra-][An example of setting for hydra]]
    + [[#some-examples-of-defining-specific-emms-simple-players-of-mpv][Some examples of defining specific emms simple players of mpv]]
  + [[#references][References]]

** Requirements

   + GNU Emacs 24 or later
   + EMMS 4.0 or later
   + mpv v0.7 or later ( [[https://github.com/mpv-player/mpv]] )
   + Unix Sockets

   Tested with GNU Emacs 24.5.1 (emacs-mac-app @5.9_0), EMMS 4.0, mpv @0.9.2_1.

** Setup

   #+BEGIN_SRC emacs-lisp
     (add-to-list 'load-path "/path/to/emms-player-simple-mpv")
     (require 'emms-player-simple-mpv)
     ;; If you use other control functions,
     (require 'emms-player-simple-mpv-control-functions)
   #+END_SRC

** Usage

   #+BEGIN_SRC emacs-lisp
     ;; An example of setting like emms-player-mplayer.el
     ;; `emms-player-my-mpv' is defined in this case.
     (define-emms-simple-player-mpv my-mpv '(file url streamlist playlist)
         (concat "\\`\\(http[s]?\\|mms\\)://\\|"
                 (apply #'emms-player-simple-regexp
                        "aac" "pls" "m3u"
                        emms-player-base-format-list))
         "mpv" "--no-terminal" "--force-window=no" "--audio-display=no")

     (emms-player-simple-mpv-add-to-converters
      'emms-player-my-mpv "." '(playlist)
      (lambda (track-name) (format "--playlist=%s" track-name)))

     (add-to-list 'emms-player-list 'emms-player-my-mpv)
   #+END_SRC

**** Customization

***** Macro: =define-emms-simple-player-mpv= /name/ /types/ /regex/ /command/ /&rest/ /args/

      This macro emulates =define-emms-simple-player=. See EMMS Manual [[https://www.gnu.org/software/emms/manual/Simple-Players.html#Simple-Players][11 Simple Players]].

***** Function: =emms-player-simple-mpv-add-to-converters= /player/ /regexp/ /types/ /fn/ /&optional/ /appendp/

      This function adds a list (/regexp/ /types/ /fn/) to player's =mpv-track-name-converters=.
      /fn/ converts track name to input form
      when mpv needs specific format such as ffplay format for playing some protocols.

***** User Option: =emms-player-simple-mpv-use-volume-change-function-p=

      Default value is =t=.
      If the variable is non-nil,
      =emms-player-simple-mpv-volume-change= is used as =emms-volume-change-function= while an emms simple player of mpv is running.

***** User Option: =emms-player-simple-mpv-tq-event-pause-hook=

      Normal hook run when TQ process receives "pause" from mpv.

***** User Option: =emms-player-simple-mpv-tq-event-unpause-hook=

      Normal hook run when TQ process receives "unpause" from mpv.

***** User Option: =emms-player-simple-mpv-tq-event-playback-restart-hook=

      Normal hook run when TQ process receives "playback-restart" from mpv.

***** User Option: =emms-player-simple-mpv-tq-event-metadata-update-hook=

      Normal hook run when TQ process receives "metadata-update" from mpv.

**** An example of setting for TQ event hooks

     #+BEGIN_SRC emacs-lisp
       (require 'emms-mode-line)
       (require 'emms-playing-time)

       (defun emms-player-simple-mpv--reset-playing-time-display-timer ()
         "Reset `emms-playing-time'."
         (emms-player-simple-mpv-tq-enqueue
          '("get_property" "time-pos") nil
          (lambda (_ ans-ls)
            (let ((time (emms-player-simple-mpv-tq-assq-v 'data ans-ls)))
              (when (numberp time)
                (when emms-playing-time-display-timer
                  (emms-cancel-timer emms-playing-time-display-timer)
                  (setq emms-playing-time-display-timer nil))
                (setq emms-playing-time (1- (floor time)))
                (emms-playing-time-display)
                (force-mode-line-update t)
                (unless emms-player-paused-p
                  (setq emms-playing-time-display-timer
                        (run-at-time (- 1.0 (- time (ffloor time)))
                                     1 'emms-playing-time-display))))))))

       (add-hook 'emms-player-simple-mpv-tq-event-unpause-hook
                 'emms-player-simple-mpv--reset-playing-time-display-timer)

       (add-hook 'emms-player-simple-mpv-tq-event-playback-restart-hook
                 'emms-player-simple-mpv--reset-playing-time-display-timer)

       (defun emms-player-simple-mpv--set-current-playlist-filename
           "Set the current playlist's filenam to `emms-mode-line-string'."
         (when (eq (emms-track-type (emms-playlist-current-selected-track))
                   'playlist)
           (emms-player-simple-mpv-tq-enqueue
            '("get_property" "filename") nil
            (lambda (_ ans-ls)
              (let ((filename (emms-player-simple-mpv-tq-assq-v 'data ans-ls)))
                (when (stringp filename)
                  (setq emms-mode-line-string
                        (format emms-mode-line-format filename))
                  (force-mode-line-update t)))))))

       (add-hook 'emms-player-simple-mpv-tq-event-metadata-update-hook
                 'emms-player-simple-mpv--set-current-playlist-filename)
     #+END_SRC

**** An example of setting for hydra ( [[https://github.com/abo-abo/hydra]] )

     This emulates default key bindings of mpv player.

     #+BEGIN_SRC emacs-lisp
       (require 'hydra)
       (require 'emms-player-simple-mpv-control-functions)

       ;; (global-set-key (kbd "<f2> m") 'emms-player-simple-mpv-hydra/body)
       (defhydra emms-player-simple-mpv-hydra
         (:foreign-keys warn :hint nil)
         "
         Keyboard Control for emms simple player of mpv
       -------------------------------------------------------------
         _Q_        Quit emms-player-simple-mpv-hydra.
         ─────────────────────────────
         <left> and <right>
                  Seek backward/forward 5 seconds.
         S-<left> and S-<right>
                  Seek backward/forward 1 seconds.
         <down> and <up>
                  Seek backward/forward 1 minute.
         S-<down> and S-<upt>
                  Seek backward/forward 5 seconds.
         ─────────────────────────────
         \[ and \]  Decrease/increase current playback speed by 10 %%%%.
         \{ and \}  Halve/double current playback speed.
         <backspace>
                  Reset playback speed to normal.
         ─────────────────────────────
         < and >  Go backward/forward in the playlist.
         <retrun> Go forward in the playlist.
         ─────────────────────────────
         p / SPC  Pause (pressing again unpauses).
         ─────────────────────────────
         q        Stop playing and quit.
         ─────────────────────────────
         / and *  Decrease/increase volume.
         9 and 0  Decrease/increase volume.
         ─────────────────────────────
         m        Mute sound.
         ─────────────────────────────
         f        Toggle fullscreen.
         ─────────────────────────────
         T        Toggle stay-on-top.
         ─────────────────────────────
         l        Set/clear A-B loop points.
       -------------------------------------------------------------

       "
         ("Q" nil)
         ("<left>"    (lambda () (interactive) (emms-seek -5)))
         ("S-<left>"  (lambda () (interactive) (emms-seek -1)))
         ("<down>"    (lambda () (interactive) (emms-seek -60)))
         ("S-<down>"  (lambda () (interactive) (emms-seek -5)))
         ("<right>"   (lambda () (interactive) (emms-seek 5)))
         ("S-<right>" (lambda () (interactive) (emms-seek 1)))
         ("<up>"      (lambda () (interactive) (emms-seek 60)))
         ("S-<up>"    (lambda () (interactive) (emms-seek 5)))
         ("["  emms-player-simple-mpv-speed-decrease)
         ("]"  emms-player-simple-mpv-speed-increase)
         ("{"  emms-player-simple-mpv-speed-halve)
         ("}"  emms-player-simple-mpv-speed-double)
         ("<backspace>" emms-player-simple-mpv-speed-normal)
         ("<" emms-player-simple-mpv-playlist-prev)
         (">" emms-player-simple-mpv-playlist-next)
         ("<return>" emms-player-simple-mpv-playlist-next)
         ("p" emms-pause)
         ("SPC" emms-pause)
         ("q" (lambda () (interactive)
                (when (y-or-n-p "emms-stop")
                  (emms-stop))) :exit t)
         ("/" emms-volume-lower)
         ("*" emms-volume-raise)
         ("9" emms-volume-lower)
         ("0" emms-volume-raise)
         ("m" emms-player-simple-mpv-mute)
         ("f" emms-player-simple-mpv-fullscreen)
         ("T" emms-player-simple-mpv-ontop)
         ("l" emms-player-simple-mpv-ab-loop))
     #+END_SRC

**** Some examples of defining specific emms simple players of mpv

     + [[https://github.com/momomo5717/emms-player-mpv-jp-radios]]

** References

     + emms-player-mpv ( [[https://github.com/dochang/emms-player-mpv]] )
     + mpv.el ( [[https://github.com/kljohann/mpv.el]] )
